import {
  Ext,
  List,
  Magic,
  Mem,
  Word,
  this.box,
  this.c-size {C-Size, from-c-size},
  this.foreign {THREAD_COND_SIZE, THREAD_MUTEX_SIZE},
  this.int {sum-int},
  this.pointer {shift},
  this.unit {unit},
}

inline _get-clone(a: type, x: &a): a {
  *x
}

nominal {
  _cell: type,
}

data cell(_: type) {
| _Cell(_cell)
}

define _new(a: type, val: a): cell(a) {
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  let cond-size = from-c-size(THREAD_COND_SIZE) in
  let u = Word.size in
  let ptr = Ext.malloc(C-Size(sum-int([u, mutex-size, cond-size, u]))) in
  Mem.store-int(Magic.cast(a, int, val), ptr);
  let _ = Ext.thread-mutex-init(shift(ptr, u)) in
  Ext.thread-cond-init(shift(ptr, add-int(u, mutex-size)));
  Mem.store-int(Magic.cast(type, int, a), shift(ptr, add-int(u, add-int(mutex-size, cond-size))));
  Magic.cast(pointer, cell(a), ptr)
}

// (content, thread-mutex, thread-cond)
resource _cell {
  function (ch: int) {
    let ch = magic cast(int, pointer, ch) in
    let u = Word.size in
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
    let cond-size = from-c-size(THREAD_COND_SIZE) in
    let cond-offset = add-int(u, mutex-size) in
    let type-offset = add-int(u, add-int(mutex-size, cond-size)) in
    let a = Magic.cast(int, type, Mem.load-int(shift(ch, type-offset))) in
    Ext.thread-cond-destroy(shift(ch, cond-offset));
    Ext.thread-mutex-destroy(shift(ch, u));
    let _: a = magic cast(int, a, Mem.load-int(ch)) in
    Ext.free(ch);
    0
  },
  function (ch: int) {
    let ch = magic cast(int, pointer, ch) in
    let u = Word.size in
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
    let cond-size = from-c-size(THREAD_COND_SIZE) in
    let type-offset = add-int(u, add-int(mutex-size, cond-size)) in
    let a = Magic.cast(int, type, Mem.load-int(shift(ch, type-offset))) in
    let val: &a = Magic.cast(int, &a, Mem.load-int(ch)) in
    let val-clone: a = _get-clone(a, val) in
    Magic.cast(cell(a), int, _new(a, val-clone))
  },
}

define _new-cell<a>(x: a): cell(a) {
  _new(a, x)
}

define mutate<a>(ch: &cell(a), f: (a) -> a): unit {
  let ch = Magic.cast(&cell(a), pointer, ch) in
  let u = Word.size in
  let mutex-ptr = shift(ch, u) in
  Ext.thread-mutex-lock(mutex-ptr);
  let val-base: int = Mem.load-int(ch) in
  let val: a = Magic.cast(int, a, val-base) in
  let new-val = f(val) in
  Mem.store-int(Magic.cast(a, int, new-val), ch);
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  Ext.thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  Ext.thread-mutex-unlock(mutex-ptr)
}

define borrow<a>(ch: &cell(a), f: (&a) -> unit): unit {
  let ch = Magic.cast(&cell(a), pointer, ch) in
  let u = Word.size in
  let mutex-ptr = shift(ch, u) in
  Ext.thread-mutex-lock(mutex-ptr);
  let val-base: int = Mem.load-int(ch) in
  let val: &a = Magic.cast(int, &a, val-base) in
  f(val);
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  Ext.thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  Ext.thread-mutex-unlock(mutex-ptr)
}

define read<a>(ch: &cell(a)): &a {
  let ch = Magic.cast(&cell(a), pointer, ch) in
  let val-base = Mem.load-int(ch) in
  Magic.cast(int, &a, val-base)
}

define clone<a>(ch: &cell(a)): a {
  let ch = Magic.cast(&cell(a), pointer, ch) in
  let u = Word.size in
  let mutex-ptr = shift(ch, u) in
  Ext.thread-mutex-lock(mutex-ptr);
  let val-base: int = Mem.load-int(ch) in
  let val: &a = Magic.cast(int, &a, val-base) in
  let result = *val in
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  Ext.thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  Ext.thread-mutex-unlock(mutex-ptr);
  result
}

define extract<a>(c: cell(a)): a {
  let u = Word.size in
  let ch = Magic.cast(cell(a), pointer, c) in
  let result = Magic.cast(int, a, Mem.load-int(ch)) in
  Ext.thread-mutex-destroy(shift(ch, u));
  Ext.thread-cond-destroy(shift(ch, add-int(u, from-c-size(THREAD_MUTEX_SIZE))));
  Ext.free(ch);
  result
}
