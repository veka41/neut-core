import {
  Magic,
  Mem,
  Word,
  this.bool,
  this.control {loop},
  this.either {from-option},
  this.external {free, malloc},
  this.int.ord {as-ord-N},
  this.list {Cons, Nil, iterate, list, minimum},
  this.unit {Unit, unit},
}

data _vecInfo {
| _VecInfo(
    word-size: int,
    from-ptr: int,
    to-ptr: int,
    type-ptr: int,
    content-ptr: int,
  )
}

define _discard-vector-content(ptr: int, elem-type: type, counter: int, size: int): unit {
  if eq-int(counter, size) {
    Unit
  } else {
    let u = Word.size in
    let _ = Magic.cast(int, elem-type, Mem.load-int(add-int(ptr, mul-int(counter, u)))) in
    _discard-vector-content(ptr, elem-type, add-int(counter, 1), size)
  }
}

define _copy-vector-content(source-pointer: int, target-pointer: int, elem-type: type, counter: int, size: int): unit {
  if eq-int(counter, size) {
    Unit
  } else {
    let u = Word.size in
    let original = Magic.cast(int, &elem-type, Mem.load-int(add-int(source-pointer, mul-int(counter, u)))) in
    let clone = *original in
    Mem.store-int(Magic.cast(elem-type, int, clone), add-int(target-pointer, mul-int(counter, u)));
    _copy-vector-content(source-pointer, target-pointer, elem-type, add-int(counter, 1), size)
  }
}

nominal {
  vector-internal: type,
}

define vector(_: type): type {
  vector-internal
}

define _get-vec-info<a>(xs: &vector(a)): _vecInfo {
  let from-ptr = Magic.cast(&vector(a), int, xs) in
  let word-size = Word.size in
  let to-ptr = add-int(from-ptr, word-size) in
  let type-ptr = add-int(from-ptr, mul-int(word-size, 2)) in
  let content-ptr = add-int(from-ptr, mul-int(word-size, 3)) in
  _VecInfo of {word-size, from-ptr, to-ptr, type-ptr, content-ptr}
}

resource vector-internal {
  function (ptr: int) {
    let base-ptr = Magic.cast(int, &vector(int), ptr) in
    let _VecInfo of {from-ptr, to-ptr, type-ptr, content-ptr} = _get-vec-info(base-ptr) in
    let from: int = Mem.load-int(from-ptr) in
    let to: int = Mem.load-int(to-ptr) in
    let size = sub-int(to, from) in
    let elem-type = Magic.cast(int, type, Mem.load-int(type-ptr)) in
    _discard-vector-content(content-ptr, elem-type, 0, size);
    Magic.cast(unit, int, free(from-ptr))
  },
  function (ptr: int) {
    let base-ptr = Magic.cast(int, &vector(int), ptr) in
    let _VecInfo of {word-size, from-ptr, to-ptr, type-ptr, content-ptr} = _get-vec-info(base-ptr) in
    let from: int = Mem.load-int(from-ptr) in
    let to: int = Mem.load-int(to-ptr) in
    let size = sub-int(to, from) in
    let elem-type: type = Magic.cast(int, type, Mem.load-int(type-ptr)) in
    let target-ptr = malloc(mul-int(word-size, add-int(size, 3))) in
    Mem.store-int(0, target-ptr);
    Mem.store-int(size, add-int(target-ptr, word-size));
    Mem.store-int(Magic.cast(type, int, elem-type), add-int(target-ptr, mul-int(word-size, 2)));
    _copy-vector-content(content-ptr, add-int(target-ptr, mul-int(word-size, 3)), elem-type, 0, size);
    target-ptr
  },
}

define new<a>(base-list: list(a)): vector(a) {
  let word-size = Word.size in
  let n on base-list = this.list.length(base-list) in
  let ptr0 = malloc(mul-int(word-size, add-int(3, n))) in
  let ptr1 = add-int(ptr0, word-size) in
  let ptr2 = add-int(ptr0, mul-int(word-size, 2)) in
  let ptr3 = add-int(ptr0, mul-int(word-size, 3)) in
  Mem.store-int(0, ptr0);
  Mem.store-int(n, ptr1);
  Mem.store-int(Magic.cast(type, int, a), ptr2);
  let store-values =
    define self(values: list(a), index: int): unit {
      match values {
      | Nil =>
        Unit
      | Cons(v, rest) =>
        Mem.store-int(Magic.cast(a, int, v), add-int(ptr3, mul-int(index, word-size)));
        self(rest, add-int(index, 1))
      }
    }
  in
  let _ = store-values(base-list, 0) in
  magic cast(int, _, ptr0)
}

inline access<a>(xs: &vector(a), i: int): a {
  let _VecInfo of {from-ptr, content-ptr, word-size} = _get-vec-info(xs) in
  let from = Mem.load-int(from-ptr) in
  let elem-address = add-int(content-ptr, mul-int(word-size, add-int(from, i))) in
  let val = Magic.cast(int, &a, Mem.load-int(elem-address)) in
  *val
}

inline access-N<a>(xs: &vector(a), i: int): &a {
  let _VecInfo of {from-ptr, content-ptr, word-size} = _get-vec-info(xs) in
  let from = Mem.load-int(from-ptr) in
  let elem-address = add-int(content-ptr, mul-int(word-size, add-int(from, i))) in
  Magic.cast(int, &a, Mem.load-int(elem-address))
}

define size<a>(xs: &vector(a)): int {
  let _VecInfo of {from-ptr, to-ptr} = _get-vec-info(xs) in
  let from = Mem.load-int(from-ptr) in
  let to = Mem.load-int(to-ptr) in
  sub-int(to, from)
}

inline for-each<a>(xs: &vector(a), f: (a) -> unit): unit {
  loop(size(xs), function (i) {
    f(access(xs, i))
  })
}

inline for<a, b>(xs: &vector(a), f: (a) -> b): vector(b) {
  new of {
    base-list = {
      iterate(size(xs), function (i) {
        f(access(xs, i))
      })
    },
  }
}

inline fold-left-N<elem-type, result-type>(
  acc: result-type,
  xs: &vector(elem-type),
  consumer: (result-type, &elem-type) -> result-type,
): result-type {
  let sz = size(xs) in
  let helper =
    define loop(cursor: int, acc, xs) {
      if eq-int(cursor, sz) {
        acc
      } else {
        let y = access-N(xs, cursor) in
        loop(add-int(cursor, 1), consumer(acc, y), xs)
      }
    }
  in
  helper(0, acc, xs)
}

define zip-with<a, b, c>(xs: &vector(a), ys: &vector(b), f: (a, b) -> c): vector(c) {
  let len1 = size(xs) in
  let len2 = size(ys) in
  let min-func = minimum(as-ord-N) in
  let len-or-none = min-func([len1, len2]) in
  let len = from-option(0, len-or-none) in
  let zip-with-helper =
    define self(cursor: int) {
      if eq-int(cursor, len) {
        []
      } else {
        let val1 = access(xs, cursor) in
        let val2 = access(ys, cursor) in
        Cons(f(val1, val2), self(add-int(cursor, 1)))
      }
    }
  in
  new(zip-with-helper(0))
}
