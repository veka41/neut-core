import {
  this.bool {bool},
  this.either {Right, none},
  this.unit,
  this.word,
}

foreign {
  llvm.ctlz.i64(int64): int64,
}

// Data.IntMap.Strict in Haskell
data intmap(a) {
| Nil
| Tip(int, a)
| Bin(
    prefix: int,
    mask: int,
    left: intmap(a),
    right: intmap(a),
  )
}

constant _Key: tau {
  int
}

constant _Mask: tau {
  int
}

constant _Prefix: tau {
  int
}

// empty
inline empty<a>(): intmap(a) {
  Nil
}

inline singleton<a>(k: int, x: a): intmap(a) {
  Tip(k, x)
}

inline _mask-w(i: int, m: int): int {
  and-int(i, xor-int(mul-int(-1, m), m))
}

inline _mask(key: _Key, mask: _Mask): int {
  _mask-w(key, mask)
}

inline _zero(i: _Key, m: _Mask): bool {
  eq-int(0, and-int(i, m))
}

inline _nomatch(i: _Key, p: _Prefix, m: _Mask): bool {
  ne-int(_mask(i, m), p)
}

inline _link-with-mask<a>(m: _Mask, p1: _Prefix, t1: intmap(a), t2: intmap(a)): intmap(a) {
  let p = _mask(p1, m) in
  if _zero(p1, m) {
    Bin(p, m, t1, t2)
  } else {
    Bin(p, m, t2, t1)
  }
}

// from `Utils.Containers.Internal.BitUtil`
inline _highest-bit-mask(w: int): int {
  let size-in-bits = mul-int(this.word.size, 8) in
  shl-int(1, sub-int(sub-int(size-in-bits, 1), magic external llvm.ctlz.i64(w)))
}

inline _branch-mask(p1: _Prefix, p2: _Prefix): _Mask {
  _highest-bit-mask(xor-int(p1, p2))
}

inline _link<a>(p1: _Prefix, t1: intmap(a), p2: _Prefix, t2: intmap(a)): intmap(a) {
  _link-with-mask(_branch-mask(p1, p2), p1, t1, t2)
}

define insert<a>(k: _Key, x: a, t: intmap(a)): intmap(a) {
  match t {
  | Nil =>
    Tip(k, x)
  | Tip(ky, v) =>
    if eq-int(k, ky) {
      Tip(ky, x)
    } else {
      _link(k, Tip(k, x), ky, Tip(ky, v))
    }
  | Bin(p, m, l, r) =>
    if _nomatch(k, p, m) {
      _link(k, Tip(k, x), p, Bin(p, m, l, r))
    } else-if _zero(k, m) {
      Bin(p, m, insert(k, x, l), r)
    } else {
      Bin(p, m, l, insert(k, x, r))
    }
  }
}

define lookup<a>(k: _Key, t: &intmap(a)): ?&a {
  case t {
  | Nil =>
    none()
  | Tip(kx, x) =>
    if eq-int(k, *kx) {
      Right(x)
    } else {
      none()
    }
  | Bin(_, m, l, r) =>
    if _zero(k, *m) {
      lookup(k, l)
    } else {
      lookup(k, r)
    }
  }
}

inline _bin-check-left<a>(p: _Prefix, m: _Mask, l: intmap(a), r: intmap(a)): intmap(a) {
  match l {
  | Nil =>
    r
  | l =>
    Bin(p, m, l, r)
  }
}

inline _bin-check-right<a>(p: _Prefix, m: _Mask, l: intmap(a), r: intmap(a)): intmap(a) {
  match r {
  | Nil =>
    l
  | r =>
    Bin(p, m, l, r)
  }
}

define delete<a>(k: _Key, t: intmap(a)): intmap(a) {
  match t {
  | Nil =>
    Nil
  | Tip(ky, y) =>
    if eq-int(k, ky) {
      Nil
    } else {
      Tip(ky, y)
    }
  | Bin(p, m, l, r) =>
    if _nomatch(k, p, m) {
      Bin(p, m, l, r)
    } else-if _zero(k, m) {
      _bin-check-left(p, m, delete(k, l), r)
    } else {
      _bin-check-right(p, m, l, delete(k, r))
    }
  }
}
