import {
- this.unit [unit, Unit]
- this.bool [bool, True, False]
}

data except(a: tau, b: tau) {
- Fail(a)
- Success(b)
}

inline option(a: tau): tau {
  except(unit, a)
}

inline none-internal(a: tau): option(a) {
  Fail(Unit)
}

inline some-internal(a: tau, x: a): option(a) {
  Success(x)
}

define is-None(a: tau, x: option(a)): bool {
  match x {
  - Fail(_) =>
    True
  - Success(_) =>
    False
  }
}

define is-Some(a: tau, x: option(a)): bool {
  match x {
  - Fail(_) =>
    False
  - Success(_) =>
    True
  }
}

define from-option(a: tau, x: a, my: option(a)): a {
  match my {
  - Success(y) =>
    y
  - Fail(_) =>
    x
  }
}
