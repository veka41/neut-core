import {
  this.bool {False, True, bool},
  this.eq {Eq, eq},
  this.int8-array {_get-content-pointer, int8-array, int8-array-length},
  this.memory {load-int8},
}

constant _cmp-ptr-t: type {
  (ptr1: int, ptr2: int, index: int, to: int) -> bool
}

constant _cmp-array-t: type {
  (t1: &int8-array, t2: &int8-array) -> bool
}

inline _cmp-int8-array(cmp: (int8, int8) -> bool): _cmp-ptr-t {
  define self(ptr1: int, ptr2: int, index: int, to: int): bool {
    if eq-int(index, to) {
      True
    } else {
      let v1 = load-int8(add-int(ptr1, index)) in
      let v2 = load-int8(add-int(ptr2, index)) in
      if cmp(v1, v2) {
        self(ptr1, ptr2, add-int(index, 1), to)
      } else {
        False
      }
    }
  }
}

define _eq-int8-array(ptr1: int, ptr2: int, index: int, to: int): bool {
  _cmp-int8-array(eq-int8)(ptr1, ptr2, index, to)
}

inline cmp-int8-array(cmp: _cmp-ptr-t): _cmp-array-t {
  function (t1: &int8-array, t2: &int8-array): bool {
    let len1 = int8-array-length(t1) in
    let len2 = int8-array-length(t2) in
    if ne-int(len1, len2) {
      False
    } else {
      let ptr1 = _get-content-pointer(t1) in
      let ptr2 = _get-content-pointer(t2) in
      cmp(ptr1, ptr2, 0, len1)
    }
  }
}

define eq-int8-array(t1: &int8-array, t2: &int8-array): bool {
  cmp-int8-array(_eq-int8-array)(t1, t2)
}

constant as-eq: eq(&int8-array) {
  Eq of {equal = eq-int8-array}
}

define eq-prefix(needle: &int8-array, haystack: &int8-array): bool {
  let len1 = int8-array-length(needle) in
  let len2 = int8-array-length(haystack) in
  if lt-int(len2, len1) {
    False
  } else {
    let ptr1 = _get-content-pointer(needle) in
    let ptr2 = _get-content-pointer(haystack) in
    _eq-int8-array(ptr1, ptr2, 0, len1)
  }
}

define eq-suffix(needle: &int8-array, haystack: &int8-array): bool {
  let len1 = int8-array-length(needle) in
  let len2 = int8-array-length(haystack) in
  if lt-int(len2, len1) {
    False
  } else {
    let ptr1 = _get-content-pointer(needle) in
    let ptr2 = _get-content-pointer(haystack) in
    let ptr2' = add-int(ptr2, sub-int(len2, len1)) in
    _eq-int8-array(ptr1, ptr2', 0, len1)
  }
}

define _eq-infix(index: int, len1: int, len2: int, needle: &int8-array, haystack: &int8-array): bool {
  if lt-int(len2, add-int(index, len1)) {
    False
  } else {
    let ptr1 = _get-content-pointer(needle) in
    let ptr2 = _get-content-pointer(haystack) in
    let ptr2' = add-int(ptr2, index) in
    if _eq-int8-array(ptr1, ptr2', 0, len1) {
      True
    } else {
      _eq-infix(add-int(index, 1), len1, len2, needle, haystack)
    }
  }
}

define eq-infix(needle: &int8-array, haystack: &int8-array): bool {
  let len1 = int8-array-length(needle) in
  let len2 = int8-array-length(haystack) in
  _eq-infix(0, len1, len2, needle, haystack)
}
