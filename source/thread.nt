import {
  Ext,
  Magic,
  Mem,
  Word,
  this.bool {bool},
  this.unit {Unit, unit},
}

inline _embody(a: tau, x: &a): a {
  *x
}

inline _flow-size(): int {
  let u = Word.size in
  add-int(mul-int(u, 3), 2)
}

nominal {
  _await-and-write-result(addr: int): unit,
  flow(_: tau): tau,
  attach(a: tau, e: flow(a)): a,
}

resource flow-inner {
  function (addr: int) {
    let u = Word.size in
    let elem-type = Magic.cast(int, tau, Mem.load-int(add-int(addr, u))) in
    let pr: flow(elem-type) = Magic.cast(int, flow(elem-type), addr) in
    // await & discard
    let _: elem-type = this.thread.attach(elem-type, pr) in
    0
  },
  function (addr: int) {
    let u = Word.size in
    let u2 = mul-int(u, 2) in
    let u3 = mul-int(u, 3) in
    _await-and-write-result(addr);
    let thread-id = Mem.load-int(addr) in
    let elem-type = Magic.cast(int, tau, Mem.load-int(add-int(addr, u))) in
    let elem-value: &elem-type = Magic.cast(int, &elem-type, Mem.load-int(add-int(addr, u2))) in
    let new-addr = Ext.malloc(_flow-size()) in
    Mem.store-int(thread-id, new-addr);
    Mem.store-int(Magic.cast(tau, int, elem-type), add-int(new-addr, u));
    let cloned-value: elem-type = _embody(elem-type, elem-value) in
    Mem.store-int(Magic.cast(elem-type, int, cloned-value), add-int(new-addr, u2));
    // already finished
    Mem.store-int1(1, add-int(new-addr, u3));
    // already joined
    Mem.store-int1(1, add-int(new-addr, add-int(u3, 1)));
    new-addr
  },
}

define flow(_: tau): tau {
  flow-inner
}

define attach(a: tau, e: flow(a)): a {
  let addr = Magic.cast(flow(a), int, e) in
  _await-and-write-result(addr);
  let u = Word.size in
  let value-ptr = add-int(addr, mul-int(u, 2)) in
  let value = Mem.load-int(value-ptr) in
  Ext.free(addr);
  Magic.cast(int, a, value)
}

define _await-and-write-result(addr: int): unit {
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let u3 = mul-int(u, 3) in
  let joined-flag-ptr = add-int(addr, add-int(u3, 1)) in
  let joined-flag = Mem.load-int1(joined-flag-ptr) in
  if eq-int1(joined-flag, 0) {
    let thread-id = Mem.load-int(addr) in
    let value-ptr = add-int(addr, u2) in
    Ext.thread-join(thread-id, value-ptr);
    Mem.store-int1(1, joined-flag-ptr)
  } else {
    Unit
  }
}

// (thread-id, result-type, result-value, finished-flag, joined-flag): flow(result-type)
define detach(a: tau, f: () -> a): flow(a) {
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let u3 = mul-int(u, 3) in
  let thread-ptr = Ext.malloc(_flow-size()) in
  Mem.store-int(Magic.cast(tau, int, a), add-int(thread-ptr, u));
  Mem.store-int(0, add-int(thread-ptr, u2));
  Mem.store-int1(0, add-int(thread-ptr, u3));
  Mem.store-int1(0, add-int(thread-ptr, add-int(u3, 1)));
  let finished-flag-ptr = add-int(thread-ptr, u3) in
  let thread-func =
    function () {
      let result = f() in
      Mem.store-int1(1, finished-flag-ptr);
      Ext.thread-exit(Magic.cast(a, int, result))
    }
  in
  let closure = Magic.cast(() -> unit, int, thread-func) in
  let closure-env = Mem.load-int(add-int(closure, u)) in
  let closure-label = Mem.load-int(add-int(closure, u2)) in
  Ext.free(closure);
  let thread-id-pointer = Ext.malloc(u) in
  let thread-create =
    define naive-loop(): unit {
      let result = Ext.thread-create(thread-id-pointer, closure-label, closure-env) in
      if eq-int(result, 0) {
        Unit
      } else {
        naive-loop()
      }
    }
  in
  let _ = thread-create() in
  let thread-id = Mem.load-int(thread-id-pointer) in
  Ext.free(thread-id-pointer);
  Mem.store-int(thread-id, thread-ptr);
  Magic.cast(int, flow(a), thread-ptr)
}

define is-finished<a>(t: &flow(a)): bool {
  let u = Word.size in
  let u3 = mul-int(u, 3) in
  let addr = Magic.cast(&flow(a), int, t) in
  let finished-flag-ptr = add-int(addr, u3) in
  let finished-flag = Mem.load-int1(finished-flag-ptr) in
  eq-int1(finished-flag, 1)
}
