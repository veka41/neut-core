import {
- this.bool
- this.unit
- this.text
- this.text.io => io
- this.system
- this.arch => arch
- this.memory => mem
- this.magic => magic
- this.external => ext
- this.queue => Q
}

export {
- flow-inner
- this.thread.detach
- this.thread.attach
}

resource flow-inner {
- (p: int) => {
    // io.print("[warning] awaiting a flow to discard it\n")
    let u = arch.unit()
    let elem-type = magic.cast(int, tau, mem.load-int(add-int(p, u)))
    let pr: flow(elem-type) = magic.cast(int, flow(elem-type), p)
    let _: elem-type = this.thread.attach(elem-type, pr: flow(elem-type)) // await & discard
    0
  }
- (addr: int) => {
    // io.print("[warning] awaiting a flow to copy it\n")
    let u = arch.unit()
    let u2 = mul-int(u, 2)
    let u3 = mul-int(u, 3)
    await-and-write-result(addr)
    let thread-id = mem.load-int(addr)
    let elem-type = magic.cast(int, tau, mem.load-int(add-int(addr, u)))
    let elem-value: &elem-type = magic.cast(int, &elem-type, mem.load-int(add-int(addr, u2)))
    let new-addr = ext.malloc(flow-size())
    mem.store-int(new-addr, thread-id)
    mem.store-int(add-int(new-addr, u), magic.cast(tau, int, elem-type))
    let cloned-value: elem-type = embody(elem-type, elem-value)
    mem.store-int(add-int(new-addr, u2), magic.cast(elem-type, int, cloned-value))
    mem.store-i1(add-int(new-addr, u3), 1) // already finished
    new-addr
  }
}

inline embody(a: tau, x: &a): a {
  *x
}

inline flow-size(): int {
  let u = arch.unit()
  add-int(mul-int(u, 3), 1)
}

// (thread-id, result-type, result-value, finished-flag): flow(result-type)
define detach(a: tau, f: () -> a): flow(a) {
  let thread-func =
    () => {
      ext.thread-exit(magic.cast(a, int, f()))
    }
  let u = arch.unit()
  let u2 = mul-int(u, 2)
  let u3 = mul-int(u, 3)
  let closure = magic.cast(() -> unit, int, thread-func)
  let closure-env = mem.load-int(add-int(closure, u))
  let closure-label = mem.load-int(add-int(closure, u2))
  ext.free(closure)
  let thread-id-pointer = ext.malloc(u)
  let thread-create =
    mu naive-loop() {
      let result = ext.thread-create(thread-id-pointer, closure-label, closure-env)
      if eq-int(result, 0) {
        Unit
      } else {
        naive-loop()
      }
    }
  let _ = thread-create()
  let thread-id = mem.load-int(thread-id-pointer)
  ext.free(thread-id-pointer)
  let ptr = ext.malloc(flow-size())
  mem.store-int(ptr, thread-id)
  mem.store-int(add-int(ptr, u), magic.cast(tau, int, a))
  mem.store-int(add-int(ptr, u2), 0)
  mem.store-i1(add-int(ptr, u3), 0)
  magic.cast(int, flow(a), ptr)
}

define attach(a: tau, e: flow(a)): a {
  let addr = magic.cast(flow(a), int, e)
  await-and-write-result(addr)
  let u = arch.unit()
  let value-ptr = add-int(addr, mul-int(u, 2))
  let value = mem.load-int(value-ptr)
  ext.free(addr)
  magic.cast(int, a, value)
}

define await-and-write-result(addr: int): unit {
  let u = arch.unit()
  let u2 = mul-int(u, 2)
  let u3 = mul-int(u, 3)
  let finished-flag-ptr = add-int(addr, u3)
  let finished-flag = mem.load-i1(finished-flag-ptr)
  if eq-i1(finished-flag, 0) {
    let thread-id = mem.load-int(addr)
    let value-ptr = add-int(addr, u2)
    ext.thread-join(thread-id, value-ptr)
    mem.store-i1(finished-flag-ptr, 1)
  } else {
    Unit
  }
}
