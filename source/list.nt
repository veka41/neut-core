/// list.nt

import {
- this.bool
- this.option
- this.function
- this.product
}

export {
- list {..}
- reverse
- length
- append
- fold-left
- fold-right
- map
- concat
- unzip
- uncons
- all
- any
- range
- filter-some
}

variant list(a: tau) {
- Nil
- Cons(a, list(a))
}

define fold-left[a, b](f: (b, a) -> b, acc: b, xs: list(a)): b {
  match xs {
  - [] =>
    acc
  - y :< ys =>
    fold-left(f, f(acc, y), ys)
  }
}

define fold-right[a, b](f: (a, b) -> b, acc: b, xs: list(a)): b {
  match xs {
  - [] =>
    acc
  - y :< ys =>
    f(y, fold-right(f, acc, ys))
  }
}

define map[a, b](f: a -> b, xs: list(a)): list(b) {
  match xs {
  - [] =>
    []
  - y :< ys =>
    f(y) :< map(f, ys)
  }
}

define append[a](xs: list(a), ys: list(a)): list(a) {
  fold-left(flip(Cons), xs, ys)
}

define concat[a](xss: list(list(a))): list(a) {
  fold-left(append, [], xss)
}

define reverse[a](xs: list(a)): list(a) {
  reverse-helper(xs, [])
}

define reverse-helper[a](xs: list(a), acc: list(a)): list(a) {
  match xs {
  - [] =>
    acc
  - y :< ys =>
    reverse-helper(ys, y :< acc)
  }
}

define length[a](xs: &list(a)): int {
  let length-tailcall =
    define rec(acc: int, xs: &list(a)): int {
      case xs {
      - [] =>
        acc
      - _ :< ys =>
        rec(add-int(1, acc), ys)
      }
    }
  length-tailcall(0, xs)
}

define unzip[a, b](xs: list(a * b)): list(a) * list(b) {
  match xs {
  - [] =>
    tuple([], [])
  - tuple(l, r) :< rest =>
    let tuple(rest-l, rest-r) = unzip(rest)
    tuple(l :< rest-l, r :< rest-r)
  }
}

define uncons[a](xs: list(a)): ?product(a, list(a)) {
  match xs {
  - [] =>
    None
  - y :< ys =>
    Some(tuple(y, ys))
  }
}

define all(xs: list(bool)): bool {
  fold-left(and, True, xs)
}

define any(xs: list(bool)): bool {
  fold-left(or, False, xs)
}

// right-open interval
define range(from: int, to: int): list(int) {
  if eq-int(from, to) {
    []
  } else {
    from :< range(add-int(from, 1), to)
  }
}

define filter-some[a](xs: list(option(a))): list(a) {
  match xs {
  - [] =>
    []
  - Some(v) :< rest =>
    v :< filter-some(rest)
  - None :< rest =>
    filter-some(rest)
  }
}
