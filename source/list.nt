import {
- this.bool => [bool, True, False, and, or]
- this.both
- this.either => [option]
- this.function => [flip]
- this.data.semigroup => s
- this.data.monoid => m
- this.data.functor => f
}

data list(a: tau) {
- Nil
- Cons(a, list(a))
}

define fold-left of {
- elem-type: tau
- result-type: tau
- consumer: (result-type, elem-type) -> result-type
- acc: result-type
- xs: list(elem-type)
}: result-type {
  let loop =
    mu rec(acc: result-type, xs: list(elem-type)) {
      match xs {
      - [] =>
        acc
      - y :: ys =>
        rec(consumer(acc, y), ys)
      }
    }
  in
  loop(acc, xs)
}

define fold-right of {
- elem-type: tau
- result-type: tau
- consumer: (elem-type, result-type) -> result-type
- acc: result-type
- xs: list(elem-type)
}: result-type {
  match xs {
  - [] =>
    acc
  - y :: ys =>
    consumer(y, fold-right(elem-type, result-type, consumer, acc, ys))
  }
}

inline map(a: tau, b: tau, f: a -> b, xs: list(a)): list(b) {
  map-internal(a, b, f)(xs)
}

inline for(a: tau, b: tau, xs: list(a), f: a -> b): list(b) {
  map-internal(a, b, f)(xs)
}

inline map-internal(a: tau, b: tau, f: a -> b): list(a) -> list(b) {
  mu loop(xs) {
    match xs {
    - [] =>
      []
    - y :: ys =>
      Cons(f(y), loop(ys))
    }
  }
}

define append(a: tau, xs: list(a), ys: list(a)): list(a) {
  let flip-cons = (xs, x) => { Cons(x, xs) } in
  fold-left(a, list(a), flip-cons, xs, ys)
}

define concat(a: tau, xss: list(list(a))): list(a) {
  let partial-append = (xs, ys) => { append(a, xs, ys) } in
  fold-left(list(a), list(a), partial-append, [], xss)
}

define reverse(a: tau, xs: list(a)): list(a) {
  let helper =
    mu rec(xs: list(a), acc: list(a)) {
      match xs {
      - [] =>
        acc
      - y :: ys =>
        rec(ys, y :: acc)
      }
    }
  in
  helper(xs, [])
}

define length(a: tau, xs: &list(a)): int {
  let length-tailcall =
    mu rec(acc: int, xs: &list(a)): int {
      case xs {
      - [] =>
        acc
      - _ :: ys =>
        rec(add-int(1, acc), ys)
      }
    }
  in
  length-tailcall(0, xs)
}

define unzip(a: tau, b: tau, xs: list(tuple(a, b))): tuple(list(a), list(b)) {
  match xs {
  - [] =>
    Tuple([], [])
  - Tuple(l, r) :: rest =>
    let Tuple(rest-l, rest-r) = unzip(a, b, rest) in
    Tuple(l :: rest-l, r :: rest-r)
  }
}

define uncons(a: tau, xs: list(a)): ?tuple(a, list(a)) {
  match xs {
  - [] =>
    None
  - y :: ys =>
    Some(Tuple(y, ys))
  }
}

define all(xs: list(bool)): bool {
  fold-left(bool, bool, and, True, xs)
}

define any(xs: list(bool)): bool {
  fold-left(bool, bool, or, False, xs)
}

// right-open interval
define range(from: int, to: int): list(int) {
  if eq-int(from, to) {
    []
  } else {
    from :: range(add-int(from, 1), to)
  }
}

define filter-some(a: tau, xs: list(option(a))): list(a) {
  match xs {
  - [] =>
    []
  - Some(v) :: rest =>
    v :: filter-some(a, rest)
  - None :: rest =>
    filter-some(a, rest)
  }
}

inline as-semigroup(a: tau): s.semigroup(list(a)) {
  s.Semigroup of {
  - append =>
    (x, y) => {
      append(a, x, y)
    }
  }
}

inline as-monoid(a: tau): m.monoid(list(a)) {
  m.Monoid of {
  - as-semigroup =>
    as-semigroup(a)
  - empty =>
    []
  }
}

inline as-functor(): f.functor(list) {
  f.Functor of {
  - map =>
    map
  }
}
