import {
- this.bool [bool, True, False, and, or]
- this.data.functor [functor, Functor]
- this.data.monoid [monoid, Monoid]
- this.data.ordered [ordered, Ordered]
- this.data.semigroup [semigroup, Semigroup]
- this.except [option]
- this.function [flip]
- this.ordering [LT, GT]
- this.pair
- this.unit [unit, Unit]
}

data list(a: tau) {
- Nil
- Cons(a, list(a))
}

define fold-left[elem-type, result-type] of {
- consumer: (result-type, elem-type) -> result-type
- acc: result-type
- xs: list(elem-type)
}: result-type {
  let loop =
    mu rec(acc: result-type, xs: list(elem-type)) {
      match xs {
      - [] =>
        acc
      - y :: ys =>
        rec(consumer(acc, y), ys)
      }
    }
  in
  loop(acc, xs)
}

define fold-right[elem-type, result-type] of {
- consumer: (elem-type, result-type) -> result-type
- acc: result-type
- xs: list(elem-type)
}: result-type {
  match xs {
  - [] =>
    acc
  - y :: ys =>
    consumer(y, fold-right(consumer, acc, ys))
  }
}

inline map[a, b](f: a -> b, xs: list(a)): list(b) {
  map-internal(a, b, f)(xs)
}

inline for-each[a, b](xs: list(a), f: a -> b): list(b) {
  map-internal(a, b, f)(xs)
}

inline for[a](xs: list(a), f: a -> unit): unit {
  let _ = map-internal(a, unit, f)(xs) in
  Unit
}

inline map-internal(a: tau, b: tau, f: a -> b): list(a) -> list(b) {
  mu loop(xs) {
    match xs {
    - [] =>
      []
    - y :: ys =>
      Cons(f(y), loop(ys))
    }
  }
}

define map-noetic[a, b](f: &a -> b, xs: &list(a)): list(b) {
  case xs {
  - [] =>
    []
  - y :: ys =>
    f(y) :: map-noetic(f, ys)
  }
}

define append[a](xs: list(a), ys: list(a)): list(a) {
  fold-right(Cons, ys, xs)
}

define concat[a](xss: list(list(a))): list(a) {
  let partial-append = (xs, ys) => { append(xs, ys) } in
  fold-left(partial-append, [], xss)
}

define reverse[a](xs: list(a)): list(a) {
  let helper =
    mu rec(xs: list(a), acc: list(a)) {
      match xs {
      - [] =>
        acc
      - y :: ys =>
        rec(ys, y :: acc)
      }
    }
  in
  helper(xs, [])
}

define length[a](xs: &list(a)): int {
  let length-tailcall =
    mu rec(acc: int, xs: &list(a)): int {
      case xs {
      - [] =>
        acc
      - _ :: ys =>
        rec(add-int(1, acc), ys)
      }
    }
  in
  length-tailcall(0, xs)
}

define unzip[a, b](xs: list(tuple(a, b))): tuple(list(a), list(b)) {
  match xs {
  - [] =>
    Tuple([], [])
  - Tuple(l, r) :: rest =>
    let Tuple(rest-l, rest-r) = unzip(rest) in
    Tuple(l :: rest-l, r :: rest-r)
  }
}

define uncons[a](xs: list(a)): ?tuple(a, list(a)) {
  match xs {
  - [] =>
    None
  - y :: ys =>
    Some(Tuple(y, ys))
  }
}

define all(xs: list(bool)): bool {
  fold-left(and, True, xs)
}

define any(xs: list(bool)): bool {
  fold-left(or, False, xs)
}

// right-open interval
define range(from: int, to: int): list(int) {
  if eq-int(from, to) {
    []
  } else {
    from :: range(add-int(from, 1), to)
  }
}

define filter-some[a](xs: list(option(a))): list(a) {
  match xs {
  - [] =>
    []
  - Some(v) :: rest =>
    v :: filter-some(rest)
  - None :: rest =>
    filter-some(rest)
  }
}

inline minimum[a](as-ord: ordered(a)): list(a) -> ?a {
  (xs) => {
    let Ordered of { order } = as-ord in
    let helper =
      mu self(cand: a, ys: list(a)): a {
        match ys {
        - [] =>
          cand
        - z :: zs =>
          match order(cand, z) {
          - LT =>
            self(cand, zs)
          - _ =>
            self(z, zs)
          }
        }
      }
    in
    match xs {
    - [] =>
      None
    - y :: ys =>
      Some(helper(y, ys))
    }
  }
}

inline maximum[a](as-ord: ordered(a)): list(a) -> ?a {
  (xs) => {
    let Ordered of { order } = as-ord in
    let helper =
      mu self(cand: a, ys: list(a)): a {
        match ys {
        - [] =>
          cand
        - z :: zs =>
          match order(cand, z) {
          - GT =>
            self(cand, zs)
          - _ =>
            self(z, zs)
          }
        }
      }
    in
    match xs {
    - [] =>
      None
    - y :: ys =>
      Some(helper(y, ys))
    }
  }
}


inline as-semigroup[a](): semigroup(list(a)) {
  Semigroup of {
  - append =>
    (x, y) => {
      append(x, y)
    }
  }
}

inline as-monoid[a](): monoid(list(a)) {
  Monoid of {
  - as-semigroup =>
    as-semigroup()
  - empty =>
    []
  }
}

inline as-functor(): functor(list) {
  Functor of {
  - map =>
    (_, _, f, xs) => { map(f, xs) }
  }
}
