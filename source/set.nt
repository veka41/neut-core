import {
  Ord {ord},
  this.bool {False, True, bool},
  this.dictionary {dictionary, empty, singleton},
  this.either {Left, Right},
  this.unit {Unit, unit},
}

inline set(a): type {
  dictionary(a, unit)
}

data trope(k) {
| Trope(
    empty: set(k),
    singleton: (k) -> set(k),
    insert: (k, set(k)) -> set(k),
    lookup: (&k, &set(k)) -> bool,
    delete: (k, set(k)) -> set(k),
  )
}

inline from-ord<k>(_ord: ord(&k)): trope(k) {
  use this.dictionary.from-ord(_ord) {insert, lookup, delete} in
  Trope of {
    empty = empty(),
    singleton = {
      function (k) {
        singleton(k, Unit)
      }
    },
    insert = {
      function (k, s) {
        insert(k, Unit, s)
      }
    },
    lookup = {
      function (k, s) {
        match lookup(k, s) {
        | Right(_) =>
          True
        | Left(_) =>
          False
        }
      }
    },
    delete = exact delete,
  }
}
