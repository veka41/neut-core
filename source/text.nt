import {
- this.int8-array => [int8-array, int8-array-new, unsafe-copy-int8-array, int8-array-length]
- this.bool
- this.either
- this.list => L
- this.unit => [unit, Unit]
- this.arch => arch
- this.external => ext
- this.memory => mem
- this.magic => magic
}

alias-opaque text {
  int8-array
}

define create(xs: L.list(int8)): text {
  magic cast(int8-array, text, int8-array-new(xs))
}

define empty(): text {
  create([])
}

define append(t1: &text, t2: &text): text {
  let p1 = magic.cast(&text, int, t1)
  let p2 = magic.cast(&text, int, t2)
  let from1 = mem.load-int(p1)
  let from2 = mem.load-int(p2)
  let u = arch.unit()
  let u2 = mul-int(u, 2)
  let to1 = mem.load-int(add-int(p1, u))
  let to2 = mem.load-int(add-int(p2, u))
  let len1 = sub-int(to1, from1)
  let len2 = sub-int(to2, from2)
  let new-len = add-int(len1, len2)
  let content1 = add-int(p1, u2)
  let content2 = add-int(p2, u2)
  let new-text = ext.malloc(add-int(new-len, u2))
  mem.store-int(0, new-text)
  mem.store-int(new-len, add-int(new-text, u))
  let store-values =
    mu store-values(target-pointer: int, content-array: int, current-index: int, array-length: int): unit {
      if eq-int(current-index, array-length) {
        Unit
      } else {
        let char = mem.load-int8(add-int(content-array, current-index))
        mem.store-int8(char, add-int(target-pointer, current-index))
        store-values(target-pointer, content-array, add-int(current-index, 1), array-length)
      }
    }

  let str-pointer = add-int(new-text, u2)
  store-values(str-pointer, add-int(content1, from2), 0, len1)
  store-values(add-int(str-pointer, len1), add-int(content2, from2), 0, len2)
  magic.cast(int, text, new-text)
}

define unsafe-get-content-pointer(t: &text): int {
  let ptr = magic.cast(&text, int, t)
  let from = mem.load-int(ptr)
  let u = arch.unit()
  add-int(add-int(ptr, mul-int(u, 2)), from)
}

define get-text-length-of-int(x: int, acc: int): int {
  if lt-int(x, 0) {
    get-text-length-of-int(mul-int(x, -1), add-int(acc, 1))
  } else {
    let q = div-int(x, 10)
    if eq-int(q, 0) {
      add-int(acc, 1)
    } else {
      get-text-length-of-int(q, add-int(acc, 1))
    }
  }
}

inline text-byte-length(t: &text): int {
  let ptr = magic cast(&text, int8-array, t)
  int8-array-length(ptr)
}

define from-int(x: int): text {
  let u = arch.unit()
  let u2 = mul-int(u, 2)
  let len = get-text-length-of-int(x, 0)
  let ptr = ext.malloc(add-int(len, u2))
  mem.store-int(0, ptr)
  mem.store-int(len, add-int(ptr, u))
  let content = add-int(ptr, u2)
  let store-positive-values =
    mu loop(y: int, index: int): unit {
      if lt-int(y, 10) {
        let digit-char = add-int8(magic.cast(int, int8, y), 48) // 48 == "0"
        mem.store-int8(digit-char, add-int(content, index))
      } else {
        let q = div-int(y, 10)
        let r = rem-int(y, 10)
        let digit-char = add-int8(magic.cast(int, int8, r), 48)
        mem.store-int8(digit-char, add-int(content, index))
        loop(q, sub-int(index, 1))
      }
    }

  if lt-int(x, 0) {
    mem.store-int8(45, content) // 45 == "-"
    store-positive-values(mul-int(x, -1), sub-int(len, 1))
  } else {
    store-positive-values(x, sub-int(len, 1))
  }
  magic.cast(int, text, ptr)
}

define unsafe-from-c-string(old-ptr: int): text {
  let u = arch.unit()
  let u2 = mul-int(u, 2)
  let len = unsafe-get-c-string-length(old-ptr, 0)
  let new-ptr = ext.malloc(add-int(len, u2))
  mem.store-int(0, new-ptr)
  mem.store-int(len, add-int(new-ptr, u))
  unsafe-copy-int8-array(old-ptr, add-int(new-ptr, u2), 0, 0, len)
  magic.cast(int, text, new-ptr)
}

define unsafe-get-c-string-length(ptr: int, index: int): int {
  let c = mem.load-int8(add-int(ptr, index))
  if eq-int8(c, 0) {
    index
  } else {
    unsafe-get-c-string-length(ptr, add-int(index, 1))
  }
}
