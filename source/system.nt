import {
- this.bool => [bool]
- this.list => [list]
- this.text => [text, unsafe-from-c-string]
- this.text.io => [print]
- this.unit => [unit, Unit]
- this.external => [exit]
- this.memory => [load-int]
- this.arch => arch
}

define admit(a: tau, t: &text): a {
  print(t);
  exit(a, 1)
}

inline assert(message: &text, check: () -> bool): unit {
  introspect build-mode {
  - release =>
    let _ = check in
    let _ = message in
    Unit
  - default =>
    assert-inner(message, check)
  }
}

define assert-inner(message: &text, check: () -> bool): unit {
  if check() {
    Unit
  } else {
    print(message)
  }
}

define get-argc(): int {
  let argc-ptr: int = magic global("neut-unsafe-argc", pointer) in
  let argc: int = load-int(argc-ptr) in
  argc
}

define get-argv(): list(text) {
  let argc = get-argc() in
  let argv-ptr: int = magic global("neut-unsafe-argv", pointer) in
  let argv: int = load-int(argv-ptr) in
  let u = arch.unit() in
  let f =
    mu get-argv-helper(i: int): list(text) {
      if eq-int(i, argc) {
        []
      } else {
        let rest = get-argv-helper(add-int(i, 1)) in
        let str-ptr-ptr = add-int(argv, mul-int(i, u)) in
        let str-ptr = load-int(str-ptr-ptr) in
        let t = unsafe-from-c-string(str-ptr) in
        t :: rest
      }
    }
  in
  f(0)
}
