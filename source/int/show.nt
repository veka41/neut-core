import {
  Ext,
  Word,
  this.c-int {c-int, from-c-int},
  this.c-size {c-size},
  this.memory {store-int},
  this.pointer {null-pointer, shift},
  this.show {Show, show},
  this.text {_get-content-pointer, text},
  this.unit,
}

foreign {
  snprintf(pointer, c-size, pointer): c-int,
  sprintf(pointer, pointer): c-int,
}

define _show(c-fmt: &text, x: int): text {
  let fmt = _get-content-pointer(c-fmt) in
  let size = from-c-int(magic external snprintf(null-pointer, 0, fmt)(x: int)) in
  let ptr = Ext.malloc(add-int(size, add-int(1, mul-int(Word.size, 2)))) in
  store-int(0, ptr);
  // ignore the trailing null character
  store-int(size, shift(ptr, Word.size));
  let buffer = shift(ptr, mul-int(Word.size, 2)) in
  let _ = magic external sprintf(buffer, fmt)(x: int) in
  magic cast(pointer, text, ptr)
}

constant as-show: show(int) {
  Show(function (x) {
    _show("%ld", x)
  })
}

define show-int(x: int): text {
  as-show::show(x)
}

define show-uint(x: int64): text {
  _show("%lu", x)
}
